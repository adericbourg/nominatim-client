plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id 'maven-publish'
}

group 'net.dericbourg'
version Git.getVersion()

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    implementation 'org.slf4j:slf4j-api:1.7.32'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
}

publishing {
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/adericbourg/nominatim-client"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}


class Git {

    static String getVersion() {
        if (isOnTag()) {
            return getCurrentTag()
        } else {
            return "${new Date().format('yyyyMMddHHmmss')}-${getGitHash()}-SNAPSHOT"
        }
    }

    static String getCurrentTag() {
        return "git describe --tags --exact-match".execute().text.trim()
    }

    static Boolean isOnTag() {
        return !getCurrentTag().isBlank()
    }

    static String getGitHash() {
        return "git rev-parse --short HEAD".execute().text.trim()

    }
}
